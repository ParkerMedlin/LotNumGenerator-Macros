
Public Function RoundUp(ByVal value As Double)
    If Int(value) = value Then
        RoundUp = value
    Else
        RoundUp = Int(value) + 1
    End If
End Function

Sub plusSign()
'///assigned to plus sign icon/////////////////////////////////////////////////////////////////////////////////////////////
'///Calls the incrementor and then the WhichLine userform//////////////////////////////////////////////////////////////////

Call increment
WhichLineDialog_LOTNUM.Show
If Range("Z1").value = "Cancelled" Then
    Range("Z1").ClearContents
    Range("J3").Select
Else
    Call copyLast
    Range("B3").Select
End If
End Sub

Sub increment()
'///Not assigned to anything///////////////////////////////////////////////////////////////////////////////////////////////
'///Inserts a new line and handles all the formula re-populating + time-stamping + incrementing lot no/////////////////////

'make an array, grab lot info, insert it in the new row, timestamp it
Call macrosOff

Dim batchInfo(6) As Variant
batchInfo(0) = Range("A2").value  'part number (convert the value to text so INDEX(MATCH()) still works)
batchInfo(0) = "'" + batchInfo(0)
batchInfo(1) = Range("B2").value 'blend desc
batchInfo(2) = Range("C2").value 'lot number
batchInfo(3) = Range("D2").value 'quantity
batchInfo(4) = RoundUp((batchInfo(3) / 250)) * 2 'label quantity
batchInfo(5) = RoundUp((batchInfo(3) / 250)) 'tote quantity

Rows("3:3").Select
Selection.Insert Shift:=xlDown, CopyOrigin:=xlFormatFromRightOrBelow
Rows("3:3").Select
    With Selection.Interior
        .Pattern = xlNone
        .TintAndShade = 0
        .PatternTintAndShade = 0
    End With
Range("A3").value = batchInfo(0) 'plug in part number
Range("B3").value = batchInfo(1) 'plug in blend desc
Range("C3").value = batchInfo(2) 'plug in lot number
Range("D3").value = batchInfo(3) 'plug in quantity
Range("H3").value = (Range("H4").value) + 1

'Copy all the formatting and formulae for I:V to new row
Range("I4:Y4").Select
Selection.Copy
Range("I3:Y3").Select
ActiveCell.PasteSpecial Paste:=xlPasteAll
Range("M3").value = batchInfo(3) 'plug in quantity in blend sheet cell
Range("Q3").value = batchInfo(4) 'plug in the label quantity
Range("E3").value = Now() 'timestamp
Range("R3").Formula2 = "=INDEX(BLNDSHTS[ShortName],MATCH(A3,BLNDSHTS[ComponentItemCode from Sage],0))"

'Increment the seed number
Range("I1").value = Range("I1").value + 1

'Cleanup
Range("B2").value = ""
Range("D2").value = ""
Range("A3").Font.Italic = False
Application.CutCopyMode = False
Call macrosOn

End Sub

Sub copyLast()
'///Assigned to square>arrow>circle icon on Lot Number Generator sheet/////////////////////////////////////////////////////
'///Horix and Drum lot numbers any% speedrun///////////////////////////////////////////////////////////////////////////////

'Tell the thing how many extra copies to make past the first one
Dim i As Integer
Dim loopCount As Integer
loopCount = Application.InputBox(Prompt:="If additional lot numbers are needed for this same blend & quantity, type in how many copies are needed. If no other copies are needed, just type in 0.", Type:=1)

Dim lotInfo(5) As Variant

'For loop that will run as many times as it is told to in the inputbox
For i = 1 To loopCount
    'Get all the info from the previous lot
    lotInfo(0) = Range("B3").value 'description
    lotInfo(1) = Range("D3").value 'lot quantity
    lotInfo(2) = Range("F3").value 'line
    lotInfo(3) = Range("S3").value 'run date
    lotInfo(4) = Range("T3").value 'run day
    
    'Put description and qty into the slot at the top
    Range("B2").value = lotInfo(0) 'plug in desc
    Range("D2").value = lotInfo(1) 'plug in qty
    
    'Do the normal increment stuff
    Call increment
    
    'Put the Line, Run Date, and Day info into the top row
    Range("F3").value = lotInfo(2) 'plug in line
    Range("S3").value = lotInfo(3) 'plug in run date
    Range("T3").value = lotInfo(4) 'plug in run day
    
    'Copy formatting from the previous lot
    Range("A4:V4").Copy
    Range("A3:V3").PasteSpecial Paste:=xlPasteFormats
    
    Dim uInput As Integer
    uInput = MsgBox("Copy to Postgres?", vbQuestion + vbYesNo)
    If uInput = vbYes Then
        Dim pyArgs As String
        
        pyArgs = "'" & Range("A3").value & "', '" & Range("B3").value & "', '" & Range("C3").value & "', '" & Range("D3").value & "', '" & Range("E3").value & "', '" & Range("F3").value & "'"
        RunPython "import lotNumPyScripts; lotNumPyScripts.lotNumtoPG(" & pyArgs & ")"
    End If
    
Next i
Application.CutCopyMode = False


End Sub


